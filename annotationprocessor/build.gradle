/*
 * Copyright (C) 2014 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
import android.databinding.LicenseCollector

apply plugin: 'java'
apply plugin: 'maven'

sourceCompatibility = 1.7
targetCompatibility = 1.7

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
}

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
    }
}

dependencies {
    compile project(":baseLibrary")
    compile project(":compiler")
    compile 'commons-codec:commons-codec:1.10'
}

uploadArchives {
    repositories {
        mavenDeployer {
            pom.artifactId = 'annotationprocessor'
        }
    }
}


task fatJar(type: Jar) {
    baseName = project.name + '-all'
    doFirst {
        def LicenseCollector allLicenses = new LicenseCollector();
        configurations.compile.getResolvedConfiguration().getResolvedArtifacts().each {
            allLicenses.add(it)
        }
        def notice = allLicenses.buildNotice()
        def noticeFile = new File(project.buildDir,'NOTICE.txt')
        println ("writing notice file to: ${noticeFile.getAbsolutePath()}")
        noticeFile << notice
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    from new File(project.buildDir,'NOTICE.txt')
    archiveName "databinding-compiler-all.jar"
    destinationDir = new File(config.prebuildFolder)
    with jar
}

task prebuild() {
    dependsOn fatJar
}


